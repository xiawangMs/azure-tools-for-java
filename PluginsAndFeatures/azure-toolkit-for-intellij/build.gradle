plugins {
    id "org.jetbrains.intellij" version "1.13.3"
    id "org.jetbrains.kotlin.jvm" version "1.7.20"
    id "de.undercouch.download" version "4.1.1"
    id "com.github.ben-manes.versions" version "0.39.0"
    id "io.freefair.aspectj.post-compile-weaving" version "6.5.1"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
}

processResources {
    filesMatching('ApplicationInsights.xml') {
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: ["applicationinsights.key": project.property("applicationinsights.key")])
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.freefair.aspectj.post-compile-weaving'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.intellij'

    group = 'com.microsoft.azuretools'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    processResources {
        duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    }

    sourceSets {
        main {
            java.srcDirs 'src/main/java'
            resources {
                srcDir 'src/main/resources'
                exclude 'bundle/**'
            }
        }
        test {
            java.srcDir 'src/test/java'
            resources {
                srcDir 'src/test/resources'
            }
        }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = javaVersion
        ajc {
            enabled = false
        }
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = javaVersion
        ajc {
            enabled = false
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    ext {
        azureToolkitLibsVersion = "0.35.0-SNAPSHOT"
        azureToolkitIdeLibsVersion = "0.35.0-SNAPSHOT"
        azureToolkitUtilsVersion = "3.79.0-SNAPSHOT"
        azureToolkitHDInsightVersion = "0.1.0"
    }

    dependencyManagement {
        imports {
            mavenBom 'com.microsoft.azure:azure-toolkit-libs:' + azureToolkitLibsVersion
            mavenBom 'com.microsoft.azure:azure-toolkit-ide-libs:' + azureToolkitIdeLibsVersion
            mavenBom 'com.microsoft.hdinsight:azure-toolkit-ide-hdinsight-libs:' + azureToolkitHDInsightVersion
            mavenBom 'com.microsoft.azuretools:utils:' + azureToolkitUtilsVersion
        }
    }

    dependencies {
        // https://mvnrepository.com/artifact/org.projectlombok/lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'com.microsoft.azure:azure-toolkit-common-lib'
        aspect "com.microsoft.azure:azure-toolkit-common-lib", {
            exclude group: "com.squareup.okhttp3", module: "okhttp"
            exclude group: "com.squareup.okhttp3", module: "okhttp-urlconnection"
            exclude group: "com.squareup.okhttp3", module: "logging-interceptor"
        }
        compileOnly 'org.jetbrains:annotations'
    }

    configurations {
        implementation.exclude module: 'slf4j-api'
        implementation.exclude module: 'log4j'
        implementation.exclude module: 'stax-api'
        implementation.exclude module: 'groovy-xml'
        implementation.exclude module: 'groovy-templates'
        implementation.exclude module: 'jna'
        cucumberRuntime {
            extendsFrom testImplementation
        }
    }

    intellij {
        version = intellij_version
    }
}

subprojects {
    buildPlugin { enabled = false }
    runIde { enabled = false }
    prepareSandbox { enabled = false }
    prepareTestingSandbox { enabled = false }
    buildSearchableOptions { enabled = false }
    patchPluginXml { enabled = false }
    publishPlugin { enabled = false }
    verifyPlugin { enabled = false }

    buildSearchableOptions.onlyIf { false }
}

intellij {
    pluginName = 'azure-toolkit-for-intellij'
    version = intellij_version
    updateSinceUntilBuild = Boolean.valueOf(updateVersionRange)
    plugins = ['java', 'maven', 'maven-model', 'gradle', dep_plugins, "properties", 'terminal', 'gradle-java']
    downloadSources = Boolean.valueOf(sources)
}

sourceSets {
    main {
        java.srcDirs 'src/main/java'
        kotlin.srcDirs 'src/main/kotlin'
        resources {
            srcDir 'src/main/resources'
            exclude 'bundle/**'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
        kotlin.srcDirs 'src/test/kotlin'
        resources {
            srcDir 'src/test/resources'
        }
    }
}

dependencies {
    implementation project(path: ':azure-intellij-plugin-lib', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-lib-java', configuration: 'instrumentedJar')
    implementation project(path: ':azure-sdk-reference-book', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-resource-connector-lib', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-resource-connector-lib-java', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-resource-connector-aad', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-vm', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-springcloud', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-storage', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-redis', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-service-explorer', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-database', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-database-java', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-appservice', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-appservice-java', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-arm', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-containerregistry', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-containerservice', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-applicationinsights', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-guidance', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-cosmos', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-bicep', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-containerapps', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-monitor', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-hdinsight', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-eventhubs', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-servicebus', configuration: 'instrumentedJar')
    implementation project(path: ':azure-intellij-plugin-facet', configuration: 'instrumentedJar')
    implementation 'com.microsoft.hdinsight:azure-toolkit-ide-hdinsight-spark-lib'
    implementation 'com.microsoft.azure:azure-toolkit-ide-appservice-lib'
    implementation 'com.microsoft.azure:azure-toolkit-applicationinsights-lib'
    implementation 'com.microsoft.azure:azure-toolkit-storage-lib'

    implementation 'com.microsoft.azure:azure-client-runtime', {
        exclude group: "com.squareup.okhttp3", module: "okhttp"
        exclude group: "com.squareup.okhttp3", module: "okhttp-urlconnection"
        exclude group: "com.squareup.okhttp3", module: "logging-interceptor"
    }
    implementation 'commons-io:commons-io'
    implementation 'org.apache.commons:commons-lang3'

    implementation 'com.microsoft.azuretools:azuretools-core', {
        exclude group: "javax.xml.bind", module: "jaxb-api"
    }
    implementation 'com.microsoft.azuretools:azure-explorer-common', {
        exclude group: "javax.xml.bind", module: "jaxb-api"
    }
    implementation 'com.microsoft.azuretools:hdinsight-node-common', {
        exclude group: "javax.xml.bind", module: "jaxb-api"
    }

    implementation group: 'org.dom4j', name: 'dom4j', {
        exclude group: "javax.xml.stream", module: "stax-api"
        exclude group: "xpp3", module: "xpp3"
        exclude group: "pull-parser", module: "pull-parser"
        exclude group: "net.java.dev.msv", module: "xsdlib"
    }
    implementation group: 'jaxen', name: 'jaxen', version: '1.2.0'

    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.22")
    testImplementation 'io.cucumber:cucumber-java:7.0.0'
    testImplementation 'io.cucumber:cucumber-junit:7.0.0'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'junit:junit'
    testImplementation 'org.mockito:mockito-core'

    testImplementation 'org.powermock:powermock-module-junit4:1.7.0RC4'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.0RC4'
    testImplementation 'javax.servlet:javax.servlet-api:4.0.1'

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
}

test {
    testLogging.showStandardStreams = true
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task cucumberPackJar(type: Jar) {
    appendix = 'pathing'

    doFirst {
        manifest {
            attributes "Class-Path": configurations.cucumberRuntime.files.collect {
                it.toURI().toString().replaceFirst(/file:\/+/, '/')
            }.join(' ')
        }
    }
}

buildSearchableOptions.onlyIf { false }

task cucumber() {
    dependsOn assemble, testClasses, compileTestJava, cucumberPackJar
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = files(sourceSets.main.output, sourceSets.test.output, cucumberPackJar.archivePath)
            args = [
                    '--plugin', 'progress',
                    '--glue', 'com.microsoft.azure.hdinsight.spark.common',
                    '-m',
                    'Test/resources']
        }
    }
}

test.dependsOn cucumber
// buildPlugin.dependsOn test

defaultTasks 'buildPlugin'

wrapper() {
    gradleVersion = '7.5.1'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

//apply plugin: BundleBuildIDEAPlugin

//installIdea.dependsOn buildPlugin
//installIdea.dependsOn checkstyleMain
String timestamp = (new Date()).format('yyyyMMddHHmmss')
ext.getPatchedVersion = {
    if (Boolean.valueOf(needPatchVersion) || pluginVersion.endsWith("SNAPSHOT")) {
        if (pluginVersion.endsWith("SNAPSHOT")) {
            def fixedPluginVersion = pluginVersion.split("-")
            return "${fixedPluginVersion[0]}-${intellijDisplayVersion}-SNAPSHOT.${timestamp}"
        } else {
            return "${pluginVersion}-${intellijDisplayVersion}-BETA.${timestamp}"
        }
    } else {
        return "${pluginVersion}-${intellijDisplayVersion}"
    }
}

patchPluginXml {
    version = getPatchedVersion()
    sinceBuild = patchPluginXmlSinceBuild
}

task prepareBicepBinariesForSandbox(type: Copy) {
    def bicepBinariesDir = file("$projectDir/build/idea-sandbox/plugins/azure-toolkit-for-intellij/bicep")
    bicepBinariesDir.deleteDir()
    from("$projectDir/azure-intellij-plugin-bicep/binaries") into("$projectDir/build/idea-sandbox/plugins/azure-toolkit-for-intellij/")
}

task downloadBicepLanguageServer(type: Copy) {
    def langServerDir = file('azure-intellij-plugin-bicep/binaries/bicep/bicep-langserver')
    if (!langServerDir.exists()) {
        logger.info('Downloading bicep language server ...')
        def zipFile = new File('azure-intellij-plugin-bicep/downloaded.zip')
        new URL('https://aka.ms/java-toolkit-bicep-ls').withInputStream { i -> zipFile.withOutputStream { it << i } }
        logger.info('Unzipping bicep language server ...')
        from zipTree(zipFile) into langServerDir
    }
}

buildPlugin {
    dependsOn(prepareBicepBinariesForSandbox)
    archiveVersion = getPatchedVersion()
    from("$projectDir/NOTICE")
    // buildPlugin zips result of `prepareSandbox`, so no need to copy again?
    // copy bicep textmate bundles/language server files to root of the plugin zip, so that we don't need to unzip them manually from jar resources
    //    from("$projectDir/azure-intellij-plugin-bicep/binaries")
}

prepareSandbox {
    dependsOn(downloadBicepLanguageServer)
    finalizedBy(prepareBicepBinariesForSandbox)
}

runIde{
    dependsOn(prepareBicepBinariesForSandbox)
    jvmArgs("-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005")
}
